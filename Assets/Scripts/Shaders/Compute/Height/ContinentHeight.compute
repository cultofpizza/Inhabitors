#pragma kernel CSMain

#include "../../../Third Party/SimplexNoise.cginc"
#include "../../../Third Party/Math.cginc"

StructuredBuffer<float3> vertices;
RWStructuredBuffer<float> heights;
uint numVertices;

float baseRoughness;
int numLayers;
float3 offset;
float roughness;
float persistence;
float strength;

float oceanFloorDepth;
float oceanFloorSmoothing;
float oceanDepthMultiplier;

[numthreads(512, 1, 1)]
void CSMain(uint id : SV_DispatchThreadID)
{
   
    if (id >= numVertices)
    {
        return;
    }
	
    float3 pos = vertices[id];
    float noiseValue = 0;
    float frequency = baseRoughness;
    float amplitude = 1;
    
    for (int i = 0; i < numLayers; i++)
    {
        noiseValue += snoise(pos * frequency + offset) * amplitude;
        frequency *= roughness;
        amplitude *= persistence;
    }
    
    noiseValue = smoothMax(noiseValue, -oceanFloorDepth, oceanFloorSmoothing);
    
    if (noiseValue < 0)
    {
        noiseValue *= 1 + oceanDepthMultiplier;
    }
    
    heights[id] = noiseValue * strength;
}